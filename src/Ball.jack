class Ball {
	field int posX, posY, speedX, speedY, speed, radius;
	field int rightBorderX, leftBorderX, leftBorderY;


	constructor Ball new(int startX, int startY, int velocityX, int velocityY) {
		let posX = startX;
        let posY = startY;
        let speedX = velocityX;
		let speedY = velocityY;
		let speed = velocityX;
		let radius = Constants.getBallRadius();
		let rightBorderX = Constants.getRightBorderX();
		let leftBorderX = Constants.getLeftBorderX();
		let leftBorderY = Constants.getLeftBorderY();
		do draw();
		return this;
	}

	// Освобожднеие памяти
	method void dispose() {
		 do Memory.deAlloc(this);
		 return;
	}
	
	// Стираем мяч
	method void erase() {
      do Screen.setColor(false);
      do Screen.drawCircle(posX, posY, radius);
      return;
   }

	// Рисуем мяч
	method void draw() {
		 do Screen.setColor(true);

		 do Screen.drawCircle(posX, posY, radius);
		 return;
	}

	// Движение мяча
	method void move() {
	
		do erase();
		if ((posX < (leftBorderX + radius)) | (posX > (rightBorderX + radius))) { let speedX = -speedX; }
        if ((posY < leftBorderY)) { let speedY = -speedY; }

		let posX = posX + speedX;
        let posY = posY + speedY;

		do draw();

		return;
	}

	// Геттеры
	method int getX() { return posX; }
    method int getY() { return posY; }
	method int getSpeedY() { return speedY; }
	method int getSpeedX() { return speedX; }

	// Сеттер скорости по Y
	method void setSpeedY(int speed) { 
		let speedY = speed;
		return;
	}

	// Сеттер скорости по X
	method void setSpeedX(int speed) { 
		let speedX = speed;
		return;
	}
}
