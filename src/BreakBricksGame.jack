// 129 - backspace
// 140 - Esc
class BreakBricksGame {
	field Ball ball;
	field Paddle paddle;
	field Bricks bricks;
	field Counter counter;
	field int rowsCount, bricksInRowCount;
	field int speed;

	constructor BreakBricksGame new() {
		do startOfTheGame();

		let ball = Ball.new(45, 210, speed, speed);
		let paddle = Paddle.new(70, 235, speed);
		let bricksInRowCount = 17;
		let counter = Counter.new();
		let bricks = Bricks.new(rowsCount, bricksInRowCount);

		do TextWriter.writeInfoAboutGame();
	
		let bricks = bricks.getBricks();
		return this;
  	}

	// Начало игры: спрашиваем желаемую скорость мяча и количество рядов блоков
	method void startOfTheGame() {
		do Output.moveCursor(0, 0);

		let speed = Keyboard.readInt(TextWriter.getEnterSpeedText());
		if ((speed > 8)) { let speed = 8; }
		let rowsCount = Keyboard.readInt(TextWriter.getEnterRowsText());
		if ((rowsCount > 19)) {let rowsCount = 19; }

		do Screen.clearScreen();
		return;
	}


	// Освобождение памяти
	method void dispose() {
		 do ball.dispose();
		 do paddle.dispose();
		 do counter.dispose();
		 do bricks.dispose();
		 do Memory.deAlloc(this);
		 return;
	}

	// Ждём, когда игрок нажмёт backspace и начинаем игру заново
	method void waitRestart() {
		while (true) {
				if (Keyboard.keyPressed() = 129) {
					do Screen.clearScreen();
					do Main.main();
				}
			}
		return;
	}

	// Пока не нажали escape, постоянно запускаем все методы проверок и движения
	method void run() {
		var char key;
		var boolean exit;
		let exit = false;
		while (~exit) {
			if (Keyboard.keyPressed() = 140)  { let exit = true; }
			do counter.draw();
			do ball.move();
			do paddle.move();
			do checkAllConditions();
		}
		do dispose();
		do TextWriter.writeExitText();
		do waitRestart();
		return;
	}

	// Проверяем столкновение мяча с платформой/кирпичиками и не конец ли игры
	method void checkAllConditions() {
		var int posY, posX;
		var int speedX, speedY;

		let posX = ball.getX();
		let posY = ball.getY();
		let speedX = ball.getSpeedX();
		let speedY = ball.getSpeedY();

		do checkPaddleCollision(posX, posY, speedY);
		do checkBrickCollision(posX, posY, speedX, speedY);
		do checkGameOver(ball);
		do checkWinning(counter);
		do Sys.wait(10);
		return;
	}

	// Проверка столкновения мяча с платформой
	method void checkPaddleCollision(int posX, int posY, int speedY) {
		var int paddleX, paddleY;
		
		let paddleX = paddle.getX();
		let paddleY = paddle.getY();

		if ((posY > (paddleY - speed)) & (posY < (paddleY + speed)) & (posX > (paddleX - 2)) & (posX < (paddleX + 25))) {
			let speedY = -speedY;
			do ball.setSpeedY(speedY);
		}				
		return;
	}

	// Проверка столкновения мяча с блоком
	method void checkBrickCollision(int posX, int posY, int speedX, int speedY) {
    var int i, j;
    var Brick brick;
    var int brickX, brickY;
    var int brickWidth, brickHeight, ballRadius; 
	var int nextPosX, nextPosY;

	let brickHeight = 10;
	let brickWidth = 25;
	let ballRadius = 5;

    let j = 0;
    while (j < rowsCount) {
        let i = 0;
        while (i < bricksInRowCount) {
            let brick = bricks[i + (j * bricksInRowCount)];

            if (~(brick = null)) {
                let brickX = brick.getX();
                let brickY = brick.getY();

				let nextPosX = posX + speedX;
                let nextPosY = posY + speedY;

                if ((((nextPosY + ballRadius) > brickY) & ((nextPosY - ballRadius) < (brickY + brickHeight)) & 
                    ((nextPosX + ballRadius) > brickX) & ((nextPosX - ballRadius) < (brickX + brickWidth)))) {

                    do ball.setSpeedY(-speedY);

                    do brick.erase();
                    do brick.dispose();

                    do counter.add();

                    let bricks[i + (j * bricksInRowCount)] = null;
                }
            }
            let i = i + 1;
        }
        let j = j + 1;
    }
    return;
}

	// Проверка на то, что игрок проиграл
	method void checkGameOver(Ball ball) {
		var int ballY;
		let ballY = ball.getY();
		if ((ballY + speed) > 249) {
			do dispose();
			do TextWriter.writeGameOver();
			do waitRestart();
		}
		return;
	}

	// Проверяем, не победили ли мы
	method void checkWinning(Counter counter) {
		var int bricksCount;
		let bricksCount = rowsCount * bricksInRowCount;
		if(counter.getCount() = bricksCount) {
			do dispose();
			do TextWriter.writeVictory();
			do waitRestart();
		}
		return;
	}

	
}